plugins {
    id "java-library"
    id "io.qameta.allure" version "2.11.2"
}

group "com.kyattonippu"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

def allureVersion = "2.23.0",
    selenideVersion = "7.0.2",
    junitVersion = "5.10.0",
    slf4jVersion = "2.0.9"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation (
            "com.github.javafaker:javafaker:1.0.2",
            "org.assertj:assertj-core:3.24.2",
            "org.aeonbits.owner:owner:1.0.12",
            "org.seleniumhq.selenium:selenium-java:3.141.59",
            "io.github.bonigarcia:webdrivermanager:5.6.2",
            "com.codeborne:selenide:$selenideVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "io.qameta.allure:allure-selenide:$allureVersion"
    )

    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task ui_tests(type: Test) {
    useJUnitPlatform {
        includeTags("UI")
    }
}

task remote_tests(type: Test) {
    useJUnitPlatform {
        includeTags("remote")
    }
}